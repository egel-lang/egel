@"Combinators to make dict more useful."

import "prelude.eg"
import "generator.eg"

namespace Dict ( 
    using System
    using List
 
    def from_list =
        @"Dict::from_list l - create a dictionary from key/value pairs"
        foldl [D (K,V) -> Dict::set D K V] dict

    def to_list =
        @"Dict::to_list d - create a list of key/value pairs from a dictionary"
        [ D -> foldl [LL K -> cons (K, Dict::get D K) LL] nil (Dict::keys D) ]

    def values =
        @"Dict::values d - create a list of values a dictionary"
        [ D -> List::map snd (to_list D) ]

    def nth_with_default =
        @"Dict::nth_with_default v d k - get a value from a dictionary with a default element"
        [ V D K -> if Dict::has D K then Dict::get D K else V ]

    def copy =
        @"Dict::copy d - create a copy of a dictionary"
        Dict::from_list . Dict::to_list 

    def update =
        @"Dict::copy d k v - change an entry when key is present"
        [ D K V -> if Dict::has D K then Dict::set D K V else D ]

    def adjust =
        @"Dict::adjust d k f - apply a function to a value in the dictionary"
        [ D K F -> if Dict::has D K then Dict::set D K (F (Dict::get D K)) else D ]

    def lazy_update =
        [ D K VF -> if Dict::has D K then Dict::set D K (VF none) else D ]

    def dmap =
        @"Dict::dmap f d - map a function to all values of a dictionary"
        [ F D -> foldl [D K -> Dict::set D K (F (Dict::get D K))] D (Dict::keys D) ]

    def merge_dicts =
        @"Dict::merge_dicts d0 d1 - produce the merger of two dictionaries"
        [ D0 D1 -> Dict::from_list ((Dict::to_list D0) ++ (Dict::to_list D1)) ]

    def inverse0 =
        @"Dict::invert d0 d1 - produce the merger of two dictionaries"
        [ {} -> {} | {(K0,V)|XX} -> 
            [ {}           -> {(K0,{V})} 
            | {(K1,VV)|XX} -> if K0 == K1 then {(K0,{V|VV})|XX} else {(K0,{V}),(K1,VV)|XX} ]
                (inverse0 XX) ]

    def inverse = 
        @"Dict::inverse d - produce the inverse of a dictionary, values map to multiple keys"
        Dict::from_list . inverse0 . sort . List::map swap . Dict::to_list
)
