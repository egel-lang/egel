# combinators to make dict more useful

import "dict.ego"
import "prelude.eg"
import "generator.eg"

namespace Dict ( 

    using List
    using System
 
    def from_list =
        foldl [D (K,V) -> Dict::set D K V] dict

    def to_list =
        [ D -> foldl [LL K -> cons (K, Dict::get D K) LL] nil (Dict::keys D) ]

    def values =
        [ D -> List::map snd (to_list D) ]

    def nth =
        [ V D K -> if Dict::has D K then Dict::get D K else V ]

    def copy =
        Dict::from_list . Dict::to_list 

    def update =
        [ D K V -> if Dict::has D K then Dict::set D K V else D ]

    def lazy_update =
        [ D K VF -> if Dict::has D K then Dict::set D K (VF none) else D ]

    def map =
        [ F D -> foldl [D K -> Dict::set D K (F (Dict::get D K))] D (Dict::keys D) ]

    def union =
        [ D0 D1 -> Dict::from_list ((Dict::to_list D0) ++ (Dict::to_list D1)) ]
)
