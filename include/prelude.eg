namespace System (

    ## System::or p q - boolean or
    def or =
        [ false false -> false
        | X Y         -> true ]

    ## System::and p q - boolean and
    def and =
        [ true true    -> true
        | X Y          -> false ]

    ## System::not p q - boolean not
    def not =
        [ true  -> false
        | X     -> true ]

    ## System::|| p q - 'lazy' or
    def || = 
        [ false F -> F none
        | true  F -> true ]

    ## System::&& p q - 'lazy' and
    def && = 
        [ true F -> F none
        | false F -> false ]

    ## System::. f g - function composition
    def . =
        [ F G X -> F (G X) ]

    ## System::|> x f - reverse application
    def |> =
        [ X F -> F X ]

    ## System::flip f x y - flip two arguments
    def flip = [ F X Y -> F Y X ]

    ## System::uncurry f (x, y) - uncurry arguments
    def uncurry = [ F (X,Y) -> F X Y ]

    ## System::iter n f x - iterate a function
    def iter = [ 0 F X -> X | N F X -> iter (N - 1) F (F X) ]

    ## System::fst (x, y) - proj0 on pair
    def fst = [ (X,Y) -> X ]

    ## System::snd (x, y) - proj1 on pair
    def snd = [ (X,Y) -> Y ]

    ## System::$ - a very mysterious operator
    #def $ = [F X -> F X]

    def @@ =
        [ SELF NAME -> (get NAME SELF) SELF ]

    ## System::abs x - absolute
    def abs = [X -> if X < 0 then 0 - X else X ]

    ## System::min x y - minimum
    def min = [X Y -> if X < Y then X else Y ]

    ## System::max x y - maximum
    def max = [X Y -> if X < Y then Y else X ]

    ## System::** x y - power (temporary)
    def ** = [ N 0 -> 1 | N M -> N * (N ** (M - 1)) ]
)

namespace Option (
    data just
    data nothing
)

namespace List (

    using System

    ## List::length l - length of a list
    def length =
        [ nil -> 0
        | (cons X XX) -> 1 + (length XX) ]

    ## List::foldl f z l - left fold on a list
    def foldl =
        [ F Z nil -> Z
        | F Z (cons X XX) -> foldl F (F Z X) XX ]

    ## List::foldr f z l - right fold on a list
    def foldr =
        [ F Z nil -> Z
        | F Z (cons X XX) -> F X (foldr F Z XX) ]

    ## List::head l - head of a list
    def head =
        [ (cons X XX) -> X ]

    ## List::tail l - tail of a list
    def tail =
        [ (cons X XX) -> XX ]

    ## List::last l - last of a list
    def last =
        [ (cons X nil) -> X 
        | (cons X XX) -> last XX ]

    ## List::init l - init of a list
    def init =
        [ (cons X nil) -> nil
        | (cons X XX)  -> cons X (init XX) ]

    ## List::++ l0 l1 - concatenation of two lists
    def ++ =
        [ nil YY -> YY
        | (cons X XX) YY -> cons X (XX ++ YY) ]

    ## List::postpend l e - postpend an element
    def postpend =
        [ nil Y -> cons Y nil
        | (cons X XX) Y -> cons X (postpend XX Y) ]

    ## List::map f l - map a function over a list
    def map =
        [ F nil -> nil
        | F (cons X XX) -> let Y = F X in cons Y (map F XX) ]

    ## List::reverse l - reverse a list
    def reverse = 
       foldl (flip cons) nil

    ## List::block n - list of number from 0 to n exclusive
    def block =
        [ 0 -> nil
        | N -> cons (N - 1) (block (N - 1)) ]

    ## List::nth n l - nth element of a list
    def nth =
        [ 0 (cons X XX) -> X
        | N (cons X XX) -> nth (N - 1) XX ]

    ## List::insert n x l - insert an element at given position
    def insert =
        [ 0 X (cons Y YY) -> cons X YY
        | I X (cons Y YY) -> cons Y (insert (I - 1) X YY) ]

    ## List::take n l - take the first n elements of a list
    def take =
        [ 0 XX -> nil
        | N nil -> nil
        | N (cons X XX) -> cons X (take (N - 1) XX) ]

    ## List::drop n l - drop the first n elements of a list
    def drop =
        [ 0 XX -> XX
        | N nil -> nil
        | N (cons X XX) -> drop (N - 1) XX ]

    ## List::chunks n l - list to list of chunks of given size
    def chunks =
        [ _ nil -> nil
        | N L   -> if 0 < N then cons (take N L) (chunks N (drop N L))
                   else nil ]

    ## List::from_to min max - list of numbers for min to max (exclusive)
    def from_to =
        [ X Y -> 
            if X <= Y then cons X (from_to (X+1) Y)
            else nil ]

    ## List::filter p l - filter all members from a list which satisfy a predicate
    def filter =
        [ P nil -> nil
        | P (cons X XX) -> if P X then cons X (filter P XX) 
                                 else filter P XX ]

    ## List::split p l - split a list into members and non-members of a predicate
    def split =
        [ P nil -> (nil, nil)
        | P (cons X XX) -> 
            [(XX0, XX1) -> if P X then (cons X XX0, XX1) else (XX0, cons X XX1) ]
                (split P XX) ]

    ## List::break p l - split a list in two parts
    def break =
        [ P nil -> (nil, nil)
        | P (cons X XX) -> if P X then (nil, cons X XX) else 
            let (YY, ZZ) = break P XX in (cons X YY, ZZ) ]

    ## List::split_on x ll - split a list on a member
    def split_on =
        [ X nil -> cons nil nil
        | X XX  -> [(YY, nil) -> {YY} | (YY,ZZ) -> {YY | split_on X (tail ZZ)}] 
                    (break ((==) X) XX) ]

    ## List::flatten ll - flatten a list of lists to a list
    def flatten =
        [ nil                   -> nil
        | (cons nil YY)         -> flatten YY
        | (cons (cons X XX) YY) -> cons X (flatten (cons XX YY)) ]

    ## List::zip l0 l1 - zip to lists to a list of pairs
    def zip =
        [ (cons X XX) (cons Y YY)  -> cons (X,Y) (zip XX YY)
        | XX YY                    -> nil ]

    ## List::zip_with f l0 l1 - apply a function pairwise to members of two lists
    def zip_with =
        [ Z (cons X XX) (cons Y YY) -> cons (Z X Y) (zip_with Z XX YY)
        | Z XX YY               -> nil ]

    ## List::transpose ll - transpose a list of lists
    def transpose =
        [ (cons nil XX) -> nil
        | XX            -> cons (map head XX) (transpose (map tail XX)) ]

    ## List::any p l - checks whether any element of a list satisfies a predicate
    def any =
        [ P nil          -> false
        | P (cons B BB)  -> if P B then true else any P BB ]

    ## List::all p l - checks whether all elements of a list  satisfies a predicate
    def all =
        [ P nil          -> true
        | P (cons B BB)  -> if P B then all P BB else false ]

    ## List::elem x l - membership test
    def elem =
        [ X -> any ((==) X) ]

    ## List::not_elem x l - inverse membership test
    def not_elem =
        [ X -> all ((/=) X) ]

    ## List::union x l - union of two lists (nˆ2 complexity)
    def union =
        [ nil YY            -> YY
        | (cons X XX) YY    -> if elem X YY then union XX YY else cons X (union XX YY) ]

    ## List::intersection x l - intersection of two lists (nˆ2 complexity)
    def intersection =
        [ nil YY            -> nil
        | (cons X XX) YY    -> if elem X YY then cons X (intersection XX YY) else intersection XX YY  ]

    ## List::unique l - make all members unique
    def unique =
        [ nil -> nil
        | (cons X XX) -> if elem X XX then unique XX else cons X (unique XX) ]

    ## List::insert_everywhere x l - insert a member in every position of a list
    def insert_everywhere =
        [ X nil -> {{X}}
        | X (cons Y YY) -> cons (cons X (cons Y YY)) (map (cons Y) (insert_everywhere X YY)) ]

    ## List::concat_map f l - concat map
    def concat_map =
        [ F -> foldr ((++) . F) nil ]

    ## List::permutations l - all permutations of a list
    def permutations =
        foldr (concat_map . insert_everywhere) {{}}

    def merge =
        [ XX nil -> XX
        | nil YY -> YY
        | (cons X XX) (cons Y YY) ->
                if X <= Y then cons X (merge XX (cons Y YY))
                else cons Y (merge (cons X XX) YY) ]

    ## List::sort l - merge sort 
    def sort =
        [ nil -> nil | (cons X nil) -> cons X nil
        | XX -> let L = length XX in
                merge (sort (take (L/2) XX)) (sort (drop (L/2) XX)) ]

    ## List::nub l - remove consecutive duplicates
    def nub =
        [ nil -> nil | (cons X nil) -> cons X nil
        | (cons X (cons Y YY)) -> if X == Y then nub (cons Y YY)
                                  else (cons X (nub (cons Y YY))) ]

    # List::sum l - summation of list
    def sum = foldl (+) 0

    # List::product l - product of list
    def product = foldl (*) 1

    # List::maximum l - maximum of list
    def maximum = foldl System::max min_int 

    # List::minimum l - minimum of list
    def minimum = foldl System::min max_int 

    ## List::range l f - iterate over elements (reverse map
    def range =
        [ XX F -> map F XX ]

    ## List::range2 l0 l1 f - iterate over elements of two lists
    def range2 =
        [ XG YG F -> flatten (range XG [X -> range YG [Y -> F X Y]]) ]

    ## List::range3 l0 l1 l2 f - iterate over elements of three lists
    def range3 =
        [ XG YG ZG F -> flatten (range XG [X -> range2 YG ZG [Y Z  -> F X Y Z]]) ]

)
